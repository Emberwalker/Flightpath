// Flightpath - build file

repositories {
  mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse' // I don't use it but meh, others might!

sourceCompatibility = 1.6
group = 'io.drakon'
version = '0.0.1'

dependencies {
  compile "com.google.code.findbugs:jsr305:1.3.9"
}

jar {
  manifest {
    attributes 'Flightpath-Version': version
  }
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourceJar
  archives javadocJar
}

// Maven
apply plugin: 'maven'

if (!project.hasProperty("DEPLOY_DIR")) {
  ext.DEPLOY_DIR = null
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${DEPLOY_DIR}")
    }
  }
}

uploadArchives.onlyIf { return DEPLOY_DIR != null }
